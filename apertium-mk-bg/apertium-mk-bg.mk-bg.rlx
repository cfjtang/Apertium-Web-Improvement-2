DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>";
 
SETS

LIST N = (n) ;
LIST Prop = (np) ;
LIST A = (adj) ;
LIST CS = (cnjsub) ;
LIST Adv = (adv) ;
LIST V = (vblex) (vbmod) (vbhaver);
LIST PREP = (pr) ;
LIST Interj = (ij) ;
LIST Pron = (prn) ;
LIST Pcle = (part) ;
LIST Copula = (vbser) ;
LIST L-part = (lp) ;
LIST P-part = (pp) ;

LIST Pers = (pers) ;
LIST Dem = (dem) ;

LIST TV = (tv) ;
LIST IV = (tv) ;

LIST Imprt = (imp) ;
LIST Pii = (pii) ;
LIST Aor = (aor) ;
LIST Prs = (pres) ;

LIST Sg = (sg) ;
LIST Pl = (pl) ;
LIST Count = (ct) ;

LIST Sg1 = (p1 sg) ;
LIST Sg2 = (p2 sg) ;
LIST Sg3 = (p3 sg) ;
LIST Pl1 = (p1 pl) ;
LIST Pl2 = (p2 pl) ;
LIST Pl3 = (p3 pl) ;

LIST Nom = (nom) ;
LIST Acc = (acc) ;
LIST Gen = (gen) ;
LIST Dat = (dat) ;
LIST Voc = (voc) ;

LIST NUMBER = sg pl ;
LIST GENDER = m f nt mfn ;
LIST PERSON = (p1 sg) (p2 sg) (p3 sg) (p1 pl) (p2 pl) (p3 pl) ;
LIST NAGDV = nom acc dat gen voc ;

SET V-IND-FIN = Prs OR Aor OR Pii ;
SET V-COP = Copula ;
SET SG2-OR-SG3 = Sg2 OR Sg3 ;
SET REL-OR-ITG = (prn rel) OR (prn itg) ;
SET N-OR-PROP = N OR Prop ;
SET S-BOUNDARY  = (prn itg) OR (prn rel) OR ("\;") OR (":") OR ("-") OR ("–") OR CS ;

###############################################################################
#  Morphological disambiguation
#

SECTION

REMOVE:r1 Interj IF ((1 V) OR (1 Pron LINK 1 V)) ;
	## Корисниците на бесплатен софтвер ќе мора да плаќаат.
	## Даниел, студент од Бања Лука, вели дека нема намера да ја напушти БиХ.

REMOVE:r2 Pron IF (0 ("не")) (-1 REL-OR-ITG) ;

REMOVE:r3 N IF (0 N) (0 V-IND-FIN) (-1C ("не") + Adv) ;

REMOVE:r4 Imprt IF (-1* REL-OR-ITG BARRIER S-BOUNDARY) ;

REMOVE:r5 TV IF (1C PREP) ;

SELECT:r6 Sg3 IF (0C V + SG2-OR-SG3) (NOT -1* Pron + Sg2 BARRIER S-BOUNDARY) ;

SELECT:r7 $$NUMBER IF (0 A) (1 N + $$NUMBER) ;

SELECT:r8 V-IND-FIN (0 V-IND-FIN) (0 Pron) (NOT -1* V-IND-FIN BARRIER S-BOUNDARY) (NOT 1* V-IND-FIN BARRIER S-BOUNDARY) ;
	## Ти си тука.

REMOVE:r9 Pron IF (0 Pron) (0 A + $$NUMBER) (1C N + $$NUMBER) ;
	## На сите места добивав ист одговор Не.

REMOVE:r10 Imprt IF (0 N) (0 Imprt) (-1C A);
	## Тешката економска во Босна и Херцеговина принуди голем број студенти и неодамна дипломирани студенти да ја напуштат земјата.

REMOVE:r11 Imprt IF (0 Imprt) (0 Prs + Sg3) ((-1 ("ќе")) OR (-1 Pron LINK -1 ("ќе")));
	## Грција објави дека ќе изврши притисок врз Турција да помогне во расчистувањето на судбината на околу 1.500 луѓе кои исчезнаа во тоа време.
	## Надминувањето на пречките ќе ги зголеми антитерористичките напори.

REMOVE:r12 Imprt IF (0 Imprt) (0 Prs + Sg3) (-1 ("да")) ; 
	## Таа спиеше еден час и почна да работи

REMOVE:r13 Imprt IF (0 Imprt) (0 Prs + Sg3) (1 CS);
	## Цитирајќи медиумски извештаи, Стејт департментот соопшти дека турските власти извршиле неколку напади на наводните ќелии на Ал каеда минатата година.

REMOVE:r14 Imprt IF (NOT 1* ("!") BARRIER S-BOUNDARY);
	# This is probably too loose, but we have to do something

REMOVE:r15 V-IND-FIN (0 V-IND-FIN) (0 N + $$NUMBER) (-1C A + $$NUMBER) ;
	## Министерот за надворешни работи, Вук Драшковиќ, во Њујорк разговараше за актуелните проблеми во покраината со генералниот секретар на ОН.

REMOVE:r16 Pron + Dem IF (0 ("тој")) (NOT 1 N);
	## Друга милитантна група ја презеде одговорноста за убиството на еден полицаец во Атина, единствениот смртоносен напад во Грција таа година.
	## "Јас студирам и патувам," рече тој.
	## Тој е тука.
	## Тој даде својата книга.

REMOVE:r17 Interj IF (0 ("да")) ((1 V OR Pron) OR (-1 V OR Pron)); 
	# Do something better here 

SELECT:r18 V-COP IF (0 V-COP) (0 V) ((1 L-part OR A OR P-part) OR (1 Pron LINK 1 L-part OR A));
	## Ако ми се јавите, би дошол.
	## Да може бебето да прозборува, би ти рекло...
	## Полицијата се сомнева дека пратката била наменета за западна Европа.
	## Кметството било укинато во 1861 година.

SELECT:r19 V-COP IF (0 V-COP) (0 V) (-1 V-COP + V-IND-FIN);
	## Ако би била една држава, ЕУ би била седмата најголема држава во светот по површина и трета најголема по население, по Кина и Индија.

SELECT:r20 A IF (0 A) (0 N) (1C N-OR-PROP) ;
	## Koсово се соочува со долг пат пред себе во спречуваoето на трговијата со дрога

SELECT:r21 A IF (0 A) (0 V-IND-FIN) (1C N-OR-PROP) ;
	## Со оглед на тоа во Западна Европа почнало сериозно да се размислува за воспоставување на поморски врски со Азија.


###############################################################################
#  Syntactic labelling and disambiguation
#

SECTION

LIST @+FMAINV = @+FMAINV;
LIST @-FMAINV = @-FMAINV;
LIST @+FAUXV = @+FAUXV;
LIST @-FAUXV = @-FAUXV;
LIST @SUBJ→ = @SUBJ→;
LIST @←SUBJ = @←SUBJ;
LIST @OBJ→ = @OBJ→;
LIST @←OBJ = @←OBJ;
LIST @IOBJ→ = @IOBJ→;
LIST @←IOBJ = @←IOBJ;
LIST @←SPRED = @←SPRED;
LIST @SPRED→ = @SPRED→;

LIST @→N = @→N;
LIST @N← = @N←;

LIST @INTERJ = @INTERJ;


MAP:r100 (@INTERJ) TARGET Interj ;

#
# Finite verb:
#    - Finite main verb
#    - Finite auxiliary (needs infinite main verb)
#    - Modal

# Main verb:
#    - Finite main verb
#    - Infinite main verb

# 
# Subjects:
#    - Personal pronouns (agree in person/number with finite main verb) 
#    - Demonstrative pronouns (finite main verb in Sg3/Pl3)
#    -
#    -


MAP:r101 (@SUBJ→) TARGET Pers + $$PERSON + Nom IF (1* V-IND-FIN + $$PERSON BARRIER V-IND-FIN) ;
	## Јас сум тука.
	## Ти си тука.
MAP:r102 (@←SUBJ) TARGET Pers + $$PERSON + Nom IF (-1* V-IND-FIN + $$PERSON BARRIER V-IND-FIN) ;


#
# Noun modifiers
#    - Adjectives (agree with head in gender/number)
#    - Numerals 
#    -

MAP:r103 (@→N) TARGET A + $$GENDER + Sg IF (1 N + $$GENDER + Sg) ;

MAP:r104 (@→N) TARGET A + Pl IF (1 N + Pl) ;

#
# Subject predicates 
#    - Adjectives in copula clauses (agree with subject in gender/number)
#

MAP:r105 (@←SPRED) TARGET A + $$NUMBER (-1* V-COP + $$NUMBER BARRIER S-BOUNDARY) ;
	## Јас сум стар.

MAP:r106 (@←SPRED) TARGET N + $$NUMBER (-1* V-COP + $$NUMBER BARRIER S-BOUNDARY OR PREP) ;
	## Јас сум македонец. 
	## Чашата е на масата.

#
# Prepositional phrases
#    - 
#    - 

MAP:r107 (@P←) TARGET N (-1 PREP); 
	## Чашата е на масата.


# Give up
#

MAP:r108 (@X) TARGET (prn dem) ;
MAP:r108 (@X) TARGET (prn pers) ;
