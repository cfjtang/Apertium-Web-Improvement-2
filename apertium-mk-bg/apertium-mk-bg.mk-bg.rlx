DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>";
 
SETS

LIST N = (n) ;
LIST A = (adj) ;
LIST CS = (cnjsub) ;
LIST Adv = (adv) ;
LIST V = (vblex) (vbser) (vbmod) ;
LIST PREP = (pr) ;
LIST Interj = (ij) ;
LIST Pron = (prn) ;
LIST Pcle = (part) ;
LIST Copula = (vbser);

LIST Pers = (pers) ;

LIST TV = (tv) ;
LIST IV = (tv) ;

LIST Imprt = (imp) ;
LIST Pii = (pii) ;
LIST Aor = (aor) ;
LIST Prs = (pres) ;

LIST Sg = (sg) ;
LIST Pl = (pl) ;
LIST Count = (ct) ;

LIST Sg1 = (p1 sg) ;
LIST Sg2 = (p2 sg) ;
LIST Sg3 = (p3 sg) ;
LIST Pl1 = (p1 pl) ;
LIST Pl2 = (p2 pl) ;
LIST Pl3 = (p3 pl) ;

LIST Nom = (nom) ;
LIST Acc = (acc) ;
LIST Gen = (gen) ;
LIST Dat = (dat) ;
LIST Voc = (voc) ;

LIST NUMBER = sg pl ;
LIST GENDER = m f nt mfn ;
LIST PERSON = (p1 sg) (p2 sg) (p3 sg) (p1 pl) (p2 pl) (p3 pl) ;
LIST NAGDV = nom acc dat gen voc ;

SET V-IND-FIN = Prs OR Aor OR Pii ;
SET V-COP = Copula ;
SET SG2-OR-SG3 = Sg2 OR Sg3 ;
SET REL-OR-ITG = (prn rel) OR (prn itg) ;
SET S-BOUNDARY  = (prn itg) OR (prn rel) OR ("\;") OR (":") OR ("-") OR ("–") OR CS ;

###############################################################################
#  Morphological disambiguation
#

SECTION

REMOVE:r1 Interj IF (1 V) ;
	## Корисниците на бесплатен софтвер ќе мора да плаќаат.

REMOVE:r2 Pron IF (0 ("не")) (-1 REL-OR-ITG) ;

REMOVE:r3 N IF (0 N) (0 V-IND-FIN) (-1C ("не") + Adv) ;

REMOVE:r4 Imprt IF (-1* REL-OR-ITG BARRIER S-BOUNDARY) ;

REMOVE:r5 TV IF (1C PREP) ;

SELECT:r6 Sg3 IF (0 V + SG2-OR-SG3) (NOT -1* Pron + Sg2 BARRIER S-BOUNDARY) ;

SELECT:r7 $$NUMBER IF (0 A) (1 N + $$NUMBER) ;

SELECT:r8 V-IND-FIN (0 V-IND-FIN) (0 Pron) (NOT -1* V-IND-FIN BARRIER S-BOUNDARY) (NOT 1* V-IND-FIN BARRIER S-BOUNDARY) ;
	## Ти си тука.

REMOVE:r9 Pron IF (0 Pron) (0 A + $$NUMBER) (1C N + $$NUMBER) ;
	## На сите места добивав ист одговор Не.

###############################################################################
#  Syntactic labelling and disambiguation
#

SECTION

LIST @+FMAINV = @+FMAINV;
LIST @-FMAINV = @-FMAINV;
LIST @+FAUXV = @+FAUXV;
LIST @-FAUXV = @-FAUXV;
LIST @SUBJ→ = @SUBJ→;
LIST @←SUBJ = @←SUBJ;
LIST @OBJ→ = @OBJ→;
LIST @←OBJ = @←OBJ;
LIST @IOBJ→ = @IOBJ→;
LIST @←IOBJ = @←IOBJ;
LIST @←SPRED = @←SPRED;
LIST @SPRED→ = @SPRED→;

LIST @→N = @→N;
LIST @N← = @N←;

LIST @INTERJ = @INTERJ;


MAP:r100 (@INTERJ) TARGET Interj ;

#
# Finite verb:
#    - Finite main verb
#    - Finite auxiliary (needs infinite main verb)
#    - Modal

# Main verb:
#    - Finite main verb
#    - Infinite main verb

# 
# Subjects:
#    - Personal pronouns (agree in person/number with finite main verb) 
#    - Demonstrative pronouns (finite main verb in Sg3/Pl3)
#    -
#    -


MAP:r101 (@SUBJ→) TARGET Pers + $$PERSON + Nom IF (1* V-IND-FIN + $$PERSON BARRIER V-IND-FIN) ;
	## Јас сум тука.
	## Ти си тука.
MAP:r102 (@←SUBJ) TARGET Pers + $$PERSON + Nom IF (-1* V-IND-FIN + $$PERSON BARRIER V-IND-FIN) ;


#
# Noun modifiers
#    - Adjectives (agree with head in gender/number)
#    - Numerals 
#    -

MAP:r103 (@→N) TARGET A + $$GENDER + Sg IF (1 N + $$GENDER + Sg) ;

MAP:r104 (@→N) TARGET A + Pl IF (1 N + Pl) ;

#
# Subject predicates 
#    - Adjectives in copula clauses (agree with subject in gender/number)
#

MAP:r105 (@←SPRED) TARGET A + $$NUMBER (-1* V-COP + $$NUMBER BARRIER S-BOUNDARY) ;
	## Јас сум стар.
	# FMT: Here we should also check for agreement in gender with the subject

MAP:r106 (@←SPRED) TARGET N + $$NUMBER (-1* V-COP + $$NUMBER BARRIER S-BOUNDARY) ;
	## Јас сум македонец. 
