DELIMITERS            = (sent);

# Nouns
LIST NC               = (n);
LIST NP               = (np top) (np ant) (np cog) (np al);

# Pronouns
LIST PrnSubj          = (prn subj);
LIST PrnDObj          = ("direct" prn obj);
LIST PrnIObj          = ("indirect" prn obj) (+indirect);  # Lemmas from sub-readings are expressed as tags and prefixed with '+'

# Determiners
LIST DetDef           = (det def);
LIST DetInd           = (det ind);
LIST DetPos           = (det pos);

# Prepositions
LIST Prep             = (pr);

# Verbal particles 
LIST Vpart            = (vpart);
LIST VpartSDO         = ("a" vpart);
LIST VpartIDO         = ("e" vpart);
LIST VpartNeg         = (vpart neg);
LIST VpartGer         = (vpart geri);
LIST VpartNegImp      = ("na" vpart neg);

# Verbs
LIST Vbloc            = (vbloc);
LIST Vblex            = (vblex);
LIST Vbser            = (vbser);

LIST VbImp            = (vblex imp);
LIST VbInf            = (vblex inf);

LIST TempsFin         = (pri) (pii) (past) (fti);

SET  Verb             = Vbloc + Vblex + Vbser;
SET  VerbFin          = Verb + TempsFin;

# Adverbs
LIST Adv              = (adv);

# Mutations
LIST SoftMut          = ("<g.*>"ri "k.*"ri) ("<d.*>"ri "t.*"ri) ("<b.*>"ri "p.*"ri) ("<c’h.*>"ri "g.*"ri) 
                        ("<w.*>"ri "gw.*"ri) ("<z.*>"ri "d.*"ri) ("<v.*>"ri "b.*"ri) ("<v.*>"ri "m.*"ri);

LIST AspMut           = ("<c’h.*>"ri "k.*"ri) ("<z.*>"ri "t.*"ri) ("<f.*>"ri "p.*"ri); 
LIST HardMut          = ("<k.*>"ri "g.*"ri) ("<t.*>"ri "d.*"ri) ("<p.*>"ri "b.*"ri); 
LIST MixedMut         = ("<c’h.*>"ri "g.*"ri) ("<w.*>"ri "gw.*"ri) ("<t.*>"ri "d.*"ri) ("<v.*>"ri "b.*"ri) ("<v.*>"ri "m.*"ri); 

LIST HardMutTrigger   = ("ho" det pos) ("e" +da det pos);

# Orthography
LIST DroppedG         = ("<[aeiou].*>"r "g[aeiou].*"ri);

# Main section


SECTION

# REGLA (B0001): "me a had"

SELECT VpartSDO IF ((-1 PrnSubj) OR (-1 PrnDObj));

# REGLA (B0002): "eviti e laboure"

SELECT VpartIDO IF ((-1 PrnIObj) OR (-1 Adv));

# REGLA (B0003): "na ganit ket"

SELECT VpartNegImp IF (1 VbImp);

# REGLA (B0004): "a had"

SELECT Verb IF (-1 Vpart);

# REGLA (B0005): "na ganit ket"

SELECT VbImp IF (-1 VpartNegImp);

# REGLA (B0006): "emañ e Brest"

SELECT Prep IF (-1 Vbloc);

# REGLA (B0008): "edo o vervel"

SELECT VpartGer IF (1 VbInf);




SECTION

# REGLA (A0001): 
SELECT SoftMut IF (-1 Vpart);

# REGLA (A0002): "hi a oar"
SELECT DroppedG IF (-1 Vpart);

# REGLA (A0003): "na ganit ket"
SELECT SoftMut IF (-1 VpartNeg);

# REGLA (A0004): "emañ e Brest"
REMOVE SoftMut IF (-1 Prep);

# REGLA (A0005): "ez di"
SELECT HardMut IF (-1 HardMutTrigger);

# REGLA (A0006): "ez di"
SELECT HardMutTrigger IF (1 HardMut);
