DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>";
SOFT-DELIMITERS = "<,>" ;
 
SETS

LIST >>> = >>> ;
LIST <<< = <<< ;

LIST N = (n) ;
LIST NP = (np) ;
LIST A = (adj) ;
LIST Det = (det) ;
LIST Num = (num) ;
LIST Adv = (adv) ;
LIST Vblex = (vblex) ;
LIST Vbmod = (vbmod) ;
LIST Vbhaver = (vbhaver) ;
LIST Copula = (vbser);
LIST Pron = (prn) ;
LIST Prep = (pr) ;
LIST CnjCoo = (cnjcoo) ;
LIST CnjSub = (cnjsub) ;
LIST CnjAdv = (cnjadv) ;
LIST Rel = (rel) ;		# PROBLEMA: està malparit en eo-es i eo-ca: prn.rel i rel.adv (es-ca: rel.adv, rel.an, rel.nn, rel.aa)
LIST Interj = (ij) ;

LIST CM = (cm) ;

SET V = Vblex + Vbmod + Vbhaver + Copula ;
SET Word = N + NP + A + Det + Adv + V + Pron + Prep + CnjCoo + CnjSub + CnjAdv + Rel + Interj ;
SET NP_Member = N + A + Det + Adv ;
SET NOT_NP_Member = Word - NP_Member;

LIST Imp = (imp) ;

LIST Pers = (pers) ;

LIST PP = (pp) ;

LIST Masc = m mf ;
LIST Fem = f mf ;
LIST Sg = sg sp ;
LIST Pl = pl sp ;
SET MascSg = (m sg) OR (m sp) OR (mf sg) OR (mf sp) ;
SET MascPl = (m pl) OR (m sp) OR (mf pl) OR (mf sp) ;
SET FemSg = (f sg) OR (f sp) OR (mf sg) OR (mf sp) ;
SET FemPl = (f pl) OR (f sp) OR (mf pl) OR (mf sp) ;

SET Cog = (np cog) ;
SET Ant = (np ant) ;

SET DetDef = (det def) ;
SET DetInd = (det ind) ;

SET Noun_Adj = N OR A ;
SET Noun_Adj_PP = N OR A OR PP ;
SET Adj_PP = A OR PP ;
SET NO_Adj_PP = Word - Adj_PP ;

SET Verb = Vblex OR Vbmod OR Vbhaver OR Copula ;
SET Verb_Prep = Verb OR Prep ;
SET Cnj_Rel = CnjCoo OR CnjSub OR CnjAdv OR Rel ;

#SET como = ("como") ;
#SET para = ("para") ;
SET Defensa = ("defensa") OR ("Defensa") ;


###############################################################################
#  Morphological disambiguation
#

SECTION

REMOVE:r_cog_1 Cog IF (-1 DetDef) (0 Cog);
REMOVE:r_cog_2 Cog IF (-1 DetInd) (0 Cog);
REMOVE:r_ant_1 Ant IF (-1 DetDef) (0 Ant);
REMOVE:r_ant_2 Ant IF (-1 DetInd) (0 Ant);

SELECT:este_2 Det IF (0 ("este") + $$MascSg) (-1 Verb_Prep) (1 A + $$MascSg) (2 N + $$MascSg) ;
	# de este bonito estado
SELECT:este_1 Det IF (0 ("este") + $$MascSg) (-1 Verb_Prep) (1 N + $$MascSg) ;
	# de este estado
SELECT:este_2 Det IF (0 ("Este") + $$MascSg) (-1 >>>) (1 A + $$MascSg) (2 N + $$MascSg) ;
	# Este bonito estado
SELECT:este_1 Det IF (0 ("Este") + $$MascSg) (-1 >>>) (1 N + $$MascSg) ;
	# Este estado

REMOVE:r_na_1_ms A OR PP IF (-1 >>>) (O Noun_Adj_PP + $$MascSg) (1 Adj_PP + $$MascSg) ;
REMOVE:r_na_1_mp A OR PP IF (-1 >>>) (O Noun_Adj_PP + $$MascPl) (1 Adj_PP + $$MascPl) ;
REMOVE:r_na_1_fs A OR PP IF (-1 >>>) (O Noun_Adj_PP + $$FemSg) (1 Adj_PP + $$FemSg) ;
REMOVE:r_na_1_fp A OR PP IF (-1 >>>) (O Noun_Adj_PP + $$FemPl) (1 Adj_PP + $$FemPl) ;
	## jóvenes inteligentes (jóvenes > n)
	## jóvenes encapuchadas (jóvenes > n)
	## abisenios inteligentes (jóvenes > n)
	## escrito largo (escrito > n)

REMOVE:r_na_2_ms N IF (-1 N + $$MascSg) (0 Adj_PP + $$MascSg) (*1 Adj_PP + $$MascSg) ;
REMOVE:r_na_2_mp N IF (-1 N + $$MascPl) (0 Adj_PP + $$MascPl) (*1 Adj_PP + $$MascPl) ;
REMOVE:r_na_2_fs N IF (-1 N + $$FemSg) (0 Adj_PP + $$FemSg) (*1 Adj_PP + $$FemSg) ;
REMOVE:r_na_2_fp N IF (-1 N + $$FemPl) (0 Adj_PP + $$FemPl) (*1 Adj_PP + $$FemPl) ;
	## "la Primera Conferencia Americana celebrada" (pqè Americana > adj)

REMOVE:r_na_2_1_ms N IF (-2 N + $$MascSg) (-1 Adj_PP + $$MascSg) (0 Adj_PP + $$MascSg) (*1 Adj_PP + $$MascSg) ;
REMOVE:r_na_2_1_mp N IF (-2 N + $$MascPl) (-1 Adj_PP + $$MascPl) (0 Adj_PP + $$MascPl) (*1 Adj_PP + $$MascPl) ;
REMOVE:r_na_2_1_fs N IF (-2 N + $$FemSg) (-1 Adj_PP + $$FemSg) (0 Adj_PP + $$FemSg) (*1 Adj_PP + $$FemSg) ;
REMOVE:r_na_2_1_fp N IF (-2 N + $$FemPl) (-1 Adj_PP + $$FemPl) (0 Adj_PP + $$FemPl) (*1 Adj_PP + $$FemPl) ;
	## "la Primera Conferencia Internacional Americana celebrada" (pqè Americana > adj)

#REMOVE:r_na_2_2_ms N IF (-3 N + $$MascSg) (-2 Adj_PP + $$MascSg) (-1 Adj_PP + $$MascSg) (0 Adj_PP + $$MascSg) (*1 Adj_PP + $$MascSg) ;
	## líder obrero norteamericano afiliado (afiliado > adj)

#REMOVE:r_na_3_ms A OR PP IF (NOT -1 N) (O Noun_Adj_PP + $$MascSg) (1 Adj_PP + $$MascSg) (*2 Adj_PP + $$MascSg BARRIER NO_Adj_PP) ; # at list one Adj_PP
REMOVE:r_na_3_ms A OR PP IF (NOT -1 N) (O Noun_Adj_PP + $$MascSg) (1 Adj_PP + $$MascSg) ;
REMOVE:r_na_3_mp A OR PP IF (NOT -1 N) (O Noun_Adj_PP + $$MascPl) (1 Adj_PP + $$MascPl) ;
REMOVE:r_na_3_fs A OR PP IF (NOT -1 N) (O Noun_Adj_PP + $$FemSg) (1 Adj_PP + $$FemSg) ;
REMOVE:r_na_3_fp A OR PP IF (NOT -1 N) (O Noun_Adj_PP + $$FemPl) (1 Adj_PP + $$FemPl) ;
	## vieron jóvenes inteligentes (jóvenes > n)
	## vieron jóvenes encapuchadas (jóvenes > n)
	## vieron abisenios inteligentes (jóvenes > n)
	## PROBLEMA: con alto contenido (alto > n/adv); ídem per a adj ambigus típicament preposats: alto, bajo, mal (+flaco favor, etc.)

SELECT:s_na_1_ms N IF (-1 Det + $$MascSg) (O Noun_Adj_PP + $$MascSg) (1 Prep OR Det) ;
SELECT:s_na_1_fs N IF (-1 Det + $$FemSg) (O Noun_Adj_PP + $$FemSg) (1 Prep OR Det) ;
SELECT:s_na_1_mp N IF (-1 Det + $$MascPl) (O Noun_Adj_PP + $$MascPl) (1 Prep OR Det) ;
SELECT:s_na_1_fp N IF (-1 Det + $$FemPl) (O Noun_Adj_PP + $$FemPl) (1 Prep OR Det) ;
	# la ofensiva de pascua
	# la política del gobierno (per error, es veu Det i no Prep)
SELECT:s_na_2n N IF (-2C N) (-1 CM) (0 Noun_Adj_PP) (1 CnjCoo) (2 Noun_Adj_PP) ; 
SELECT:s_na_3n N IF (-3 CM) (-2C N) (-1 CnjCoo) (0 Noun_Adj_PP) (NOT 1 A) ; 
SELECT:s_na_4n N IF (0 Noun_Adj_PP) (1 CM) (2C N) (1 CnjCoo) (2 Noun_Adj_PP) ; 
	# compositor, teórico y pedagogo
	# varios religiosos, pensadores y políticos
SELECT:s_na_2a A IF (-2C A) (-1 CM) (0 Noun_Adj_PP) (1 CnjCoo) (2 Noun_Adj_PP) ; 
SELECT:s_na_3a A IF (-3 CM) (-2C A) (-1 CnjCoo) (0 Noun_Adj_PP) (NOT 1 N) ; 
SELECT:s_na_4a A IF (0 Noun_Adj_PP) (1 CM) (2C A) (1 CnjCoo) (2 Noun_Adj_PP) ; 
	# rojo, verde y azul

SELECT:estado_1 N IF (-1 Det + Masc) (0 ("estado") OR ("Estado")) ;

# la tierra como medio de vida

# la Indochina francesa
#	cal treure Indoĉina n
REMOVE:r_na_3_ms Noun_Adj_PP IF (-1 Det + $$MascSg) (0 NP + $$MascSg) (1 Adj_PP + $$MascSg) ;
REMOVE:r_na_3_mp Noun_Adj_PP IF (-1 Det + $$MascPl) (0 NP + $$MascPl) (1 Adj_PP + $$MascPl) ;
REMOVE:r_na_3_fs Noun_Adj_PP IF (-1 Det + $$FemSg) (0 NP + $$FemSg) (1 Adj_PP + $$FemSg) ;
REMOVE:r_na_3_fp Noun_Adj_PP IF (-1 Det + $$FemPl) (0 NP + $$FemPl) (1 Adj_PP + $$FemPl) ;

# vía pr/n
SELECT:media_1 N IF (-3C Num) (-2 N) (-1 CnjCoo) (0 ("media")) ;
SUBSTITUTE:media_2 ("media") ("media_duono") TARGET ("media") IF (-3C Num) (-2 N) (-1 CnjCoo) (0 ("media")) ;
	# dos veces y media > kaj duono
SELECT:si_ja_1 Adv IF (0 ("sí") OR ("Sí")) (1C Verb) (NOT -1 Prep) ;
SUBSTITUTE:si_ja_2 ("sí") ("sí_ja") TARGET ("sí") IF (0 ("sí") OR ("Sí")) (1C Verb) (NOT -1 Prep) ;
	# sí comentó
	# en sí fue
	
SELECT:n_a_eos_1 N IF (0 Noun_Adj_PP LINK NOT 0 Adv) (-1C Vblex) (NOT -1 PP) (1 <<<) ;
	## detonar explosivos.

SELECT:n_a_eos_2 N IF (0 Noun_Adj_PP LINK NOT 0 NP) (-1 Det) (1 <<<) ;
	## detonar los explosivos.
	## PERO: Las clasificaciones más reconocidas internacionalmente son la francesa y la estadounidense.
	## PERO: muy poco conocida.

SELECT:n_a_eos_3 N IF (-2C N) (-1 CnjCoo) (0 Noun_Adj_PP LINK NOT 0 NP) (1 <<<) ;
	## adquiriendo una gran reputación como poeta y crítico.

# Tio estas ligita al la (mis)difinoj de accionista__n, albañil__n, gilipollas__n kaj reh/én__n
#SELECT:n_mf_1 Masc IF (0C N LINK NOT 0 Defensa) (-1C Vblex OR Prep OR ("como")) (NOT -1 PP) (1 <<< OR Prep) ;
	## cogieron rehenes.
#SELECT:n_mf_2 Masc IF (0C N LINK NOT 0 Defensa) (-1C Num) (-2C Vblex OR Prep OR ("como")) (NOT -2 PP) (1 <<< OR Prep) ;
	## cogieron tres rehenes.


REMOVE:r_como_1 Vblex (O ("como") OR ("Como")) (*-1 Vblex BARRIER Cnj_Rel) ;
REMOVE:r_como_2 Vblex (O ("como") OR ("Como")) (*1 Vblex BARRIER Cnj_Rel) ;
REMOVE:r_para_1 Vblex (O ("para") OR ("Para")) (*-1 Vblex BARRIER Cnj_Rel) ;
REMOVE:r_para_2 Vblex (O ("para") OR ("Para")) (*1 Vblex BARRIER Cnj_Rel) ;
REMOVE:r_bajo_1 Vblex (O ("bajo") OR ("Bajo")) (*-1 Vblex BARRIER Cnj_Rel) ;
REMOVE:r_bajo_2 Vblex (O ("bajo") OR ("Bajo")) (*1 Vblex BARRIER Cnj_Rel) ;
SELECT:bajo_3 Prep (O ("bajo") OR ("Bajo")) (1 Det) ;
SELECT:bajo_4 Prep (O ("bajo") OR ("Bajo")) (-1 Copula) ;
SELECT:bajo_5 Prep (O ("bajo") OR ("Bajo")) (-1C N) (1C N) ;
#SELECT:bajo_ms Prep (0 ("bajo") OR ("Bajo")) (-1C N) (NOT -1 MascSg ) (1C N) (NOT 1 MascSg) ;
	# elaborada bajo presidencia

REMOVE:r_adv Adv (0 Adv) (-1C N) (1C N) ;
	# manifestantes bajo jurisdicción

#REMOVE:np_a_ms A IF (0 NP LINK 0 (adj m sg)) (-1 Prep) (1C N) (NOT 1 MascSg) ;
#REMOVE:np_a_fs A IF (0 NP LINK 0 (adj f sg)) (-1 Prep) (1C N) (NOT 1 FemSg) ;
#REMOVE:np_a_mp A IF (0 NP LINK 0 (adj m pl)) (-1 Prep) (1C N) (NOT 1 MascPl) ;
#REMOVE:np_a_fp A IF (0 NP LINK 0 (adj f pl)) (-1 Prep) (1C N) (NOT 1 FemPl) ;
	# asignaba a Armenia territorios

# eliminación del imperativo en competición con el presente del indicativo
REMOVE:imp_1 Imp IF (O (vblex pri p3 sg) LINK 0 (vblex imp p2 sg)) ;
