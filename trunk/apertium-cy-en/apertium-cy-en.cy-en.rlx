DELIMITERS = "<.>" "<!>" "<?>"; # end-of-sentence markers

# give Apertium tags a mnemonic to be used in the rules

LIST NC = (n); 				# common nouns
LIST NP = (np top) (np ant) (np cog); 	# proper nouns (toponyms, anthroponyms, cognomens)
LIST V = vblex vbser;		 	# lexical verbs and "to be"
LIST Adv = adv; 			# adverb
LIST Num = num;				# numeral
LIST CC = cnjcoo; 			# co-ordinate conjunction
LIST CS = cnjsub;			# subordinate conjunction
LIST Interj = ij;			# interjection
LIST Pr = pr;				# preposition
LIST PrnSubj = (prn subj);		# subject pronoun

LIST P2Pl = (p2 pl);			# second person plural
LIST P3Sg = (p3 sg);

LIST DetDef = (det def);		# definite determiner (article)
LIST DetPos = (det pos);		# possessive determiner

LIST Adj = (adj sint) (adj); 		# all adjectives (synthetic + non-synthetic)

LIST MF = mf;				# masculine/feminine
LIST M = m;				# masculine
LIST F = f;				# feminine

LIST Def = def; 			# definite

LIST VblexInf = (vblex inf); 		# verb in the infinitive
LIST VblexPrs = (vblex prs); 		# verb in the present subjunctive

LIST Prep_i = ("i" pr);			# the preposition "i" (to)

# mutation for individual letters
# surface form regex, base form regex
LIST MSoftC = ("<g.*>"r "c.*"r) (N);
LIST MAspC = ("<ch.*>"r "c.*"r) (N);
LIST MNasC = ("<n.*>"r "d.*"r) (N);

SET N = NP + NC;
SET PrnSubjP3Sg = PrnSubj + P3Sg;
SET PrnSubjP2Pl = PrnSubj + P2Pl;
SET VbP2Pl = V + P2Pl;

SECTION

# choose the preposition "i" if it is followed by an infinitive
# aeth i gael y pres
SELECT Prep_i IF (1 VblexInf);

# delete a subject pronoun if preceded by an infinitive
# i'w weld fo
REMOVE PrnSubj IF (-1 VblexInf);

# delete a noun from a {noun, pronoun} cohort if preceded by an infinitive and followed by a preposition
# i'w weld fo yn y dref
REMOVE N IF (-1 V) (0 N) (0 PrnSubj) (1 Pr);

# choose a 2p.pl pronoun if it is preceded by a 2p.pl verb
# rydych chi yn mynd
SELECT PrnSubjP2Pl IF (0 N) (0 PrnSubj) (-1 VbP2Pl);

# delete a verb from a {verb, noun} cohort if preceded by "preposition + the"
# yn y golchi
REMOVE V IF (0 V) (0 N) (-1 DetDef) (-2 Pr);

# delete a subject pronoun if preceded by an adjective and followed by a proper noun
# ??
REMOVE PrnSubj IF (1 NP) (-1 Adj);

# mark a possessive determiner as being masculine if followed by soft mutation
SUBSTITUTE (mf) (m) DetPos (1 MSoftC);

# and feminine if followed by aspirate mutation.
SUBSTITUTE (mf) (f) DetPos (1 MAspC);

# ymwelwyr o tramor 
# un o hen
# Ã´l i 1880
REMOVE PrnSubj IF (0 Pr) 
		  (0 PrnSubj) 
		  ((-1 NC) OR (-1 NP) OR (-1 VblexInf) OR (-1 Adj) or (-1 Num))
		  ((1 NC) OR (1 NP) OR (1 Adj) OR (1 Num));



REMOVE V IF (-1 DetDef);

# See 1.3.36 -- we currently just drop subjunctives

REMOVE VblexPrs IF (0 V); 
